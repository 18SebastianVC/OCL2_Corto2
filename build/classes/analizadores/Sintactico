package analizadores; 
import java_cup.runtime.*;

parser code 
{:
    public int contador=0;

    public void syntax_error(Symbol s){ 
        System.out.println("Error Sintáctico en la Línea " + (s.left) +
        " Columna "+s.right+ ". No se esperaba este componente: " +s.value+"."); 
    } 

    public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception{ 
        System.out.println("Error síntactico irrecuperable en la Línea " + 
        (s.left)+ " Columna "+s.right+". Componente " + s.value + 
        " no reconocido."); 
    }  
:} 

terminal String parizq,parder;
terminal String mas,menos,dividido,por;
terminal String numero;
terminal String identificador;

non terminal S;
non terminal String E,T,F;

precedence left mas,menos;
precedence left dividido,por;

start with S; 

S::=E:a {::}
;

E::=E:a mas T:b {:
    String indice="t"+contador;
    System.out.println(indice+"="+a+"+"+b);
    contador++;
    RESULT=indice;
 :}
|E:a menos T:b {: 
    String indice="t"+contador;
    System.out.println(indice+"="+a+"-"+b);
    contador++;
    RESULT=indice;
 :}
|T:b {: 
    RESULT=b;
 :}
;

T::=T:a por F:b {: 
    String indice="t"+contador;
    System.out.println(indice+"="+a+"*"+b);
    contador++;
    RESULT=indice;
 :}
|T:a dividido F:b {:
    String indice="t"+contador;
    System.out.println(indice+"="+a+"/"+b);
    contador++;
    RESULT=indice;
 :}
|F:b {: RESULT=b;
 :}
;

F::=parizq E:a parder {:
    RESULT=a;
    :}
|   numero:a{:
    RESULT=a;
    :}
|   identificador:a{:
    RESULT=a;
    :}
;

